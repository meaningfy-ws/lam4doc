PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX lam: <http://publications.europa.eu/ontology/lam-skos-ap#>
PREFIX lamd: <http://publications.europa.eu/resources/authority/lam/>

select
?classURI
(group_concat(distinct ?rdfType; separator=", ") as ?types)
(group_concat(distinct ?authorConfigValue; separator=", ") as ?AU)
(group_concat(distinct ?resourceTypeConfigValue; separator=", ") as ?FM)
(group_concat(distinct ?collectionURI; separator=", ") as ?collections)
(group_concat(distinct ?dnClassValue; separator=", ") as ?CDM_CLASS)
(group_concat(distinct ?dcValue; separator=", ") as ?DC)
(group_concat(distinct ?ctValue; separator=", ") as ?CT)
(group_concat(distinct ?ccValue; separator=", ") as ?CC)
(group_concat(distinct ?celexClassValue; separator=", ") as ?DN_CLASS)
(group_concat(distinct ?label; separator=", ") as ?labels)
(group_concat(distinct ?note; separator=", ") as ?notes)
(group_concat(distinct ?example; separator=", ") as ?examples)

FROM <http://publications.europa.eu/resources/authority/lam/LAMLegalDocument>

{
  ?classURI a skos:Concept .
  optional {
    ?classURI lam:classifyWith ?authorPropConfig .
    ?authorPropConfig lam:path lamd:md_AU;
        sh:value ?authorConfigValue .
  }
  optional {
    ?classURI lam:classifyWith ?dnClassPropConfig .
    ?dnClassPropConfig lam:path lamd:md_CDM_CLASS;
        sh:value ?dnClassValue .
  }
  optional {
    ?classURI lam:classifyWith ?celexClassifyWith .
    ?celexClassifyWith lam:path lamd:md_DN_CLASS;
        sh:value ?celexClassValue .
  }
  optional{
    ?classURI lam:hasPropertyConfiguration ?dcPropConfig .
    ?dcPropConfig lam:path lamd:md_DC;
        sh:value ?dcValue .
  }
  optional{
    ?classURI lam:hasPropertyConfiguration ?ctPropConfig .
    ?ctPropConfig lam:path lamd:md_CT;
        sh:value ?ctValue .
  }
  optional{
    ?classURI lam:hasPropertyConfiguration ?ccPropConfig .
    ?ccPropConfig lam:path lamd:md_CC;
        sh:value ?ccValue .
  }
  optional {
    ?collectionURI skos:member ?classURI
  }
  optional {
    ?classURI lam:classifyWith ?resourceTypePropConfig .
    ?resourceTypePropConfig lam:path lamd:md_FM;
        sh:value ?resourceTypeConfigValue .
  }
  optional {
    ?classURI rdf:type ?rdfType .
  }
  optional {
    ?classURI skos:prefLabel | skos:altLabel ?label .
  }
  optional {
    ?classURI skos:editorialNote ?note
  }
  optional {
    ?classURI skos:example ?example
  }
}
group by
?classURI